
### Gramatica:
ini: reiniciar instrucciones EOF 
;
reiniciar:
    Tabla_de_Errores.reiniciar()
;
instrucciones : instrucciones instruccion
    |instruccion
;
instruccion:
    DECLARACION_LIST PTCOMA
    |DECLARACION PTCOMA
    |MODIFICACION PTCOMA
    |IMPRIMIR_VARIABLE PTCOMA
    |CREAR_TABLA PTCOMA
    |MODIFICACION_TABLA PTCOMA
    |DELETE_TABLA PTCOMA
    |AGREGAR_DATOS_COLUMNAS PTCOMA
    |IMPRIMIR_TABLAS PTCOMA
    |BEGIN_ENCAPSULADO PTCOMA
    |ACTUALIZACION_COLUMNA PTCOMA
    |TRUNCATE_TABLES PTCOMA
    |DELETE_FILA PTCOMA
    |IF_SENTENCIA PTCOMA
    |PRINT_SENTENCIA PTCOMA
    |WHILE_SENTENCIA PTCOMA
    |FLUJO PTCOMA
    |FOR_SENTENCIA PTCOMA
    |error  
;
FLUJO:
    BREAK
    |CONTINUE
;
TIPO:
    INT
    |VARCHAR
    |DOUBLE
    |BOOLEAN
    |DATE
    |NULL
;
TIPOS_DATO:  
    ENTERO 
    |FECHA 
    |DECIMAL 
    |DATO_STRING 
    |TRUE 
    |FALSE 
    |ARROBA NOMBRE_DATO 
    |NULL 
;
OPERACIONES_ARITMETICAS:
    RESTA OPERACIONES_ARITMETICAS %prec UMENOS
    |OPERACIONES_ARITMETICAS SUMA OPERACIONES_ARITMETICAS
    |OPERACIONES_ARITMETICAS RESTA OPERACIONES_ARITMETICAS
    |OPERACIONES_ARITMETICAS MULTI OPERACIONES_ARITMETICAS
    |OPERACIONES_ARITMETICAS DIVI OPERACIONES_ARITMETICAS
    |OPERACIONES_ARITMETICAS MODUL OPERACIONES_ARITMETICAS
    |PAR_IZQ OPERACIONES_ARITMETICAS PAR_DER
    |TIPOS_DATO 
    |CAST_EXPRESSION 
;
RELACIONALES:
    RELACIONALES IGUAL RELACIONALES
    |RELACIONALES DIFERENTE RELACIONALES
    |RELACIONALES MENOR_O_IGUAL RELACIONALES
    |RELACIONALES MAYOR_O_IGUAL RELACIONALES
    |RELACIONALES MENOR RELACIONALES
    |RELACIONALES MAYOR RELACIONALES
    |OPERACIONES_ARITMETICAS 
;
RELACIONALES_CON_LOGICAS:
    RELACIONALES_CON_LOGICAS AND RELACIONALES_CON_LOGICAS
    |RELACIONALES_CON_LOGICAS OR RELACIONALES_CON_LOGICAS
    |NOT RELACIONALES_CON_LOGICAS
    |RELACIONALES 
;
DECLARACION_LIST:
    DECLARACION_LIST COMA DECLARACION2
    |DECLARACION
;
DECLARACION:
    DECLARE ARROBA NOMBRE_DATO TIPO DEFAULT OPERACIONES_ARITMETICAS
    |DECLARE ARROBA NOMBRE_DATO TIPO
;
DECLARACION2:
    ARROBA NOMBRE_DATO TIPO DEFAULT OPERACIONES_ARITMETICAS
    |ARROBA NOMBRE_DATO TIPO
;
MODIFICACION:
    SET ARROBA NOMBRE_DATO IGUAL OPERACIONES_ARITMETICAS
;
IMPRIMIR_VARIABLE:
    SELECT ARROBA NOMBRE_DATO
    |SELECT CAST_EXPRESSION
    |SELECT LOWER PAR_IZQ TIPOS_DATO PAR_DER
    |SELECT UPPER PAR_IZQ TIPOS_DATO PAR_DER
    |SELECT ROUND PAR_IZQ TIPOS_DATO COMA TIPOS_DATO PAR_DER
    |SELECT LEN PAR_IZQ TIPOS_DATO PAR_DER
    |SELECT TRUNCATE PAR_IZQ TIPOS_DATO COMA TIPOS_DATO PAR_DER
    |SELECT TYPEOF PAR_IZQ TIPOS_DATO PAR_DER
    |SELECT CASE_SENTENCIA
    |SELECT CASE_BUSQUEDA_SENTENCIA
;
BEGIN_ENCAPSULADO:
    BEGIN instrucciones END
; 
CREAR_TABLA:
    CREATE TABLE NOMBRE_DATO PAR_IZQ TABLA_ATRIBUTOS PAR_DER
;
TABLA_ATRIBUTOS:
    TABLA_ATRIBUTOS COMA ATRIBUTO_TABLA
    |ATRIBUTO_TABLA
;
ATRIBUTO_TABLA:
    NOMBRE_DATO TIPO
;
MODIFICACION_TABLA:
    ALTER TABLE NOMBRE_DATO ACCIONES_MODIFICAR
;
ACCIONES_MODIFICAR:
    ACCIONES_MODIFICAR ACCION_MODIFICAR
    |ACCION_MODIFICAR
;
ACCION_MODIFICAR:
    ADD NOMBRE_DATO TIPO
    |DROP COLUMN NOMBRE_DATO
    |RENAME TO NOMBRE_DATO
    |RENAME COLUMN NOMBRE_DATO TO NOMBRE_DATO
;
DELETE_TABLA:
    DROP TABLE NOMBRE_DATO
;
AGREGAR_DATOS_COLUMNAS:
    INSERT INTO NOMBRE_DATO PAR_IZQ NOMBRES_COLUMNAS PAR_DER VALUES PAR_IZQ VALORES_COLUMNAS PAR_DER
;
NOMBRES_COLUMNAS:
    NOMBRES_COLUMNAS COMA NOMBRE_COLUMNAS
    |NOMBRE_COLUMN
;
NOMBRE_COLUMNAS:
    NOMBRE_DATO
    |CAST PAR_IZQ NOMBRE_DATO AS TIPOS_DATO PAR_DER
;
VALORES_COLUMNAS:
    VALORES_COLUMNAS COMA VALORE_COLUMNAS
    |VALORE_COLUMNAS
;
VALORE_COLUMNAS:
    OPERACIONES_ARITMETICAS
;
// Imprimir en pantalla las tablas
IMPRIMIR_TABLAS:
    SELECT MULTI FROM NOMBRE_DATO
    |SELECT ATRIBUTOS_TABLAS FROM NOMBRE_DATO
    |SELECT MULTI FROM NOMBRE_DATO WHERE CONDICION
    |SELECT ATRIBUTOS_TABLAS FROM NOMBRE_DATO WHERE CONDICION
;
ATRIBUTOS_TABLAS:
    ATRIBUTOS_TABLAS COMA ATRIBUTO_TABLAS
    |ATRIBUTO_TABLAS
;
ATRIBUTO_TABLAS:
    NOMBRE_DATO
;
// Condicion especifica para where
CONDICION:
    NOMBRE_DATO IGUAL CONDICION
    |NOMBRE_DATO DIFERENTE CONDICION
    |NOMBRE_DATO MENOR_O_IGUAL CONDICION
    |NOMBRE_DATO MAYOR_O_IGUAL CONDICION
    |NOMBRE_DATO MENOR CONDICION
    |NOMBRE_DATO MAYOR CONDICION
    |TIPOS_DATO
;
// Actualizacion de valores en las tablas en sus columnas
ACTUALIZACION_COLUMNA:
    UPDATE NOMBRE_DATO SET COLUMNAS_ATRI WHERE CONDICION
;
COLUMNAS_ATRI:
    COLUMNAS_ATRI COMA COLUMNA_ATRI
    |COLUMNA_ATRI
;
COLUMNA_ATRI:
    NOMBRE_DATO IGUAL OPERACIONES_ARITMETICAS
;
// Truncate
TRUNCATE_TABLES:
    TRUNCATE TABLE NOMBRE_DATO
;
// Delete
DELETE_FILA:
    DELETE FROM NOMBRE_DATO WHERE CONDICION
;
//CASt
CAST_EXPRESSION:
    CAST PAR_IZQ OPERACIONES_ARITMETICAS AS TIPO PAR_DER
;
// Print
PRINT_SENTENCIA:
    PRINT OPERACIONES_ARITMETICAS
    |PRINT CASE_SENTENCIA
    |PRINT CASE_BUSQUEDA_SENTENCIA
;
// Sentencias de Control
// IF
IF_SENTENCIA:
    IF RELACIONALES_CON_LOGICAS THEN BEGIN instrucciones END
    |IF RELACIONALES_CON_LOGICAS THEN instrucciones ELSE instrucciones END IF
;
// CASE SIMPLE
CASE_SENTENCIA:
    CASE NOMBRE_DATO MULTI_CASE ELSE OPERACIONES_ARITMETICAS END AS NOMBRE_DATO
    |CASE OPERACIONES_ARITMETICAS MULTI_CASE ELSE OPERACIONES_ARITMETICAS END AS NOMBRE_DATO
    |CASE NOMBRE_DATO MULTI_CASE ELSE OPERACIONES_ARITMETICAS END
;
MULTI_CASE:
    MULTI_CASE MULT_CASE
    |MULT_CASE
;
MULT_CASE:
    WHEN OPERACIONES_ARITMETICAS THEN OPERACIONES_ARITMETICAS
;
//CASE BUSQUEDA
CASE_BUSQUEDA_SENTENCIA:
    CASE MULTI_CASE_BUSQUEDA ELSE OPERACIONES_ARITMETICAS END AS NOMBRE_DATO
    |CASE MULTI_CASE_BUSQUEDA ELSE OPERACIONES_ARITMETICAS END
;
MULTI_CASE_BUSQUEDA:
    MULTI_CASE_BUSQUEDA MULT_CASE_BUSQUEDA
    |MULT_CASE_BUSQUEDA
;
MULT_CASE_BUSQUEDA:
    WHEN RELACIONALES_CON_LOGICAS THEN OPERACIONES_ARITMETICAS
;
//Sentencias Ciclicas
//While
WHILE_SENTENCIA:
    WHILE RELACIONALES_CON_LOGICAS BEGIN instrucciones END
;
//For
FOR_SENTENCIA:
    FOR ARROBA NOMBRE_DATO IN OPERACIONES_ARITMETICAS DOSPTS OPERACIONES_ARITMETICAS BEGIN instrucciones END LOOP
    |FOR ARROBA NOMBRE_DATO IN OPERACIONES_ARITMETICAS DOSPTS OPERACIONES_ARITMETICAS BEGIN instrucciones END
;